sonarqube {
  properties {
    property "sonar.projectName", "${projectTitle} :: VSTS"
    property "sonar.sources", "src/main/js"
    property "sonar.tests", "src/main/js"
    property "sonar.test.inclusions", "src/main/js/**/__tests__/**"
    property "sonar.exclusions", "src/main/js/libs/third-party/**/*,src/main/js/**/__tests__/**"
    property "sonar.eslint.reportPaths", "eslint-report.json"
  }
}

yarn {
  doFirst {
    logger.lifecycle  'vsts---->yarn_install starting'
  }
  doLast {
    logger.lifecycle  'vsts---->yarn_install ended'
  }
  inputs.file('yarn.lock').withPathSensitivity(PathSensitivity.RELATIVE)
  outputs.dir('node_modules')
  outputs.cacheIf { true }
  mustRunAfter(':server:sonar-web:yarn')
}

yarn_run {
  doFirst {
    logger.lifecycle  'vsts---->yarn_run starting'
  }
  doLast {
    logger.lifecycle  'vsts---->yarn_run ended'
  }

  ['config', 'public', 'scripts', 'src'].each {
    inputs.dir(it).withPathSensitivity(PathSensitivity.RELATIVE)
  }
  ['.babelrc', 'package.json', 'tsconfig.json'].each {
    inputs.file(it).withPathSensitivity(PathSensitivity.RELATIVE)
  }
  outputs.dir('build/webapp')
  outputs.cacheIf { true }

  args = ['build']
}
build.dependsOn(yarn_run)

def sources = fileTree(dir: "src") + fileTree(dir: "scripts") + fileTree(dir: "config")

task licenseCheckWeb(type: com.hierynomus.gradle.license.tasks.LicenseCheck) {
  source = sources
}
licenseMain.dependsOn licenseCheckWeb

task licenseFormatWeb(type: com.hierynomus.gradle.license.tasks.LicenseFormat) {
  source = sources
}
licenseFormat.dependsOn licenseFormatWeb
